plugins {
    id 'java'
    id 'idea'
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '4.0.1'
    id 'com.github.sherter.google-java-format' version '0.8'
}

version = '0.4.25'
group = 'io.github.alleriawindrunner'

apply plugin: "java"
apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

repositories {
    maven { url "https://maven.aliyun.com/repository/public/" }
}

javadoc {
    options.addStringOption("charset", "UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.32'
    testCompile group: 'commons-codec', name: 'commons-codec', version: '1.14'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2.2'
    compile group: 'com.google.code.gson', name:'gson', version:'2.9.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                description = "A java compiler for the solidity."
                scm {
                    connection = 'scm:git:https://github.com/AlleriaWindrunner/solidity-compiler-java.git'
                    developerConnection = 'scm:git:ssh:git@github.com:AlleriaWindrunner/solidity-compiler-java.git'
                    url = 'https://github.com/alleriawindrunner/solidity-compiler-java'
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }

            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}

