plugins {
    id 'java'
    id 'idea'
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '4.0.1'
    id 'com.github.sherter.google-java-format' version '0.8'
}

archivesBaseName = 'solidity-compiler-java'
group = 'io.github.alleriawindrunner'
version = '0.8.11.1'

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption("charset", "UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.32'
    testCompile group: 'commons-codec', name: 'commons-codec', version: '1.14'
    
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2.2'
    compile group: 'com.google.code.gson', name:'gson', version:'2.9.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'solidity-compiler-java'
            from components.java
            pom {
                name = 'solidity-compiler-java'
                description = 'A java compiler for the solidity.'
                url = 'https://github.com/alleriawindrunner/solidity-compiler-java'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/'
                    }
                }
                developers {
                    developer {
                        id = 'AlleriaWindrunner'
                        name = 'HUANGMING'
                        email = 'eleven.hm@vip.163.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/AlleriaWindrunner/solidity-compiler-java.git'
                    developerConnection = 'scm:git:ssh:git@github.com:AlleriaWindrunner/solidity-compiler-java.git'
                    url = 'https://github.com/alleriawindrunner/solidity-compiler-java'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}